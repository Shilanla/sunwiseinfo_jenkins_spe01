/*v8 linkscript*/
/*addr constants define*/
PROVIDE (__ram_len = 1); /* represents 1M */
PROVIDE (__stack = 0x400ffff0);
PROVIDE (RegesterNum = 9);
PROVIDE (IRQNestEnableTag = 0);
OUTPUT_ARCH(sparc)
__DYNAMIC  =  0;

MEMORY
{
  rom     : ORIGIN = 0x00000000, LENGTH = 256M
  ram     : ORIGIN = 0x40000000, LENGTH = 1M
}

/*
 * stick everything in ram (of course)
 */
SECTIONS
{
  .text :
  {
    CREATE_OBJECT_SYMBOLS
    text_start = .;
    _text_start = .;
    *(.text)
    . = ALIGN (16);

    *(.eh_frame)
    . = ALIGN (16);

   /* *(.gnu.linkonce.t*) */

    /*
     * C++ constructors
     */
    __CTOR_LIST__ = .;
    LONG((__CTOR_END__ - __CTOR_LIST__) / 4 - 2)
    *(.ctors)
    LONG(0)
    __CTOR_END__ = .;
    __DTOR_LIST__ = .;
    LONG((__DTOR_END__ - __DTOR_LIST__) / 4 - 2)
    *(.dtors)
    LONG(0)
    __DTOR_END__ = .;

    _rodata_start = . ;
    *(.rodata*)
  /*  *(.gnu.linkonce.r*) */
    _erodata = ALIGN( 0x10 ) ;

    etext = ALIGN(0x10);
    _etext = .;
    *(.init)
    *(.fini)
    *(.lit)
    *(.shdata)
    . = ALIGN (16);
    _endtext = .;
  } >ram


  .data :
  {
    data_start = .;
    _data_start = .;
    _sdata = . ;
    *(.data)
    /*
    *(.gnu.linkonce.d*)
    *(.gcc_except_table)
    */
    . = ALIGN(0x10);
    edata = .;
    _edata = .;
  } > ram


  .shbss :
  {
    *(.shbss)
  }

  .bss :
  {
    __bss_start = ALIGN(0x8);
    _bss_start = .;
    bss_start = .;
    *(.bss)
    *(COMMON)
    end = .;
    _end = ALIGN(0x8);
    __end = ALIGN(0x8);
  }  > ram


   .jcr . (NOLOAD)  : { *(.jcr) 		}

  .stab . (NOLOAD) :
  {
    [ .stab ]
  }
  .stabstr . (NOLOAD) :
  {
    [ .stabstr ]
  }
}
